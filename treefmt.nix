# `nix fmt` or `nix-shell --run treefmt` formats, `treefmt --ci` is the check. Built with nixpkgs'
# `treefmt.withConfig` so every tool is pinned here and shell.nix only carries the wrapper.
{
  pkgs,
  # nix/checks.nix: inside a build there is no evaluator for package-scripts (it instantiates the set)
  evaluates ? true,
}:
let
  llvm = pkgs.llvmPackages_23;
  # nu's own parser/type checker over each file (authoritative). Fails on any error diagnostic.
  # cell-path-types: `$rec.field` takes the field's declared type instead of `any`, so typed
  # records are checked where they are used, not just where they are built
  nu-typecheck = pkgs.writeShellScript "nu-typecheck" ''
    status=0
    for f in "$@"; do
      out=$(${pkgs.nushell}/bin/nu --no-config-file --include-path "$PWD/builder:$PWD/tests/builder/lib:$PWD/pkgs/up/uptrack/src" "--experimental-options=[cell-path-types]" --ide-check 50 "$f" | ${pkgs.jq}/bin/jq -r 'select(.type == "diagnostic" and .severity == "Error") | "\(.span.start): \(.message)"')
      if [ -n "$out" ]; then printf '%s:\n%s\n' "$f" "$out" >&2; status=1; fi
    done
    exit $status
  '';
  # statix checks one path per call
  statix-each = pkgs.writeShellScript "statix-each" ''
    status=0
    for f in "$@"; do ${pkgs.statix}/bin/statix check -c ${./statix.toml} "$f" || status=1; done
    exit $status
  '';
  # ast-grep: nu via the tree-sitter grammar nushell maintains (lints/nu/), nix is built in (lints/nix/)
  sgconfig = pkgs.writeText "sgconfig.yml" (
    builtins.toJSON {
      ruleDirs = [
        "${./lints/nu}"
        "${./lints/nix}"
      ];
      customLanguages.nu = {
        libraryPath = "${pkgs.tree-sitter-grammars.tree-sitter-nu}/parser";
        extensions = [ "nu" ];
        expandoChar = "_";
      };
    }
  );
  nuFiles = [
    "*.nu"
    "pkgs/up/uptrack/src/uptrack"
  ];
in
pkgs.treefmt.withConfig {
  settings = {
    global.excludes = [
      "flake.lock"
      "pkgs/ll/llvm/*.txt"
      "result*"
      "**/lock.json"
    ];
    formatter = {
      nix = {
        command = "${pkgs.nixfmt-rs}/bin/nixfmt";
        includes = [ "*.nix" ];
      };
      nix-statix = {
        command = "${statix-each}";
        includes = [ "*.nix" ];
      };
      nix-deadnix = {
        command = "${pkgs.deadnix}/bin/deadnix";
        options = [
          "--fail"
          "--edit"
        ];
        includes = [ "*.nix" ];
      };
      go = {
        command = "${pkgs.go}/bin/gofmt";
        options = [ "-w" ];
        includes = [ "pkgs/*/*/src/*.go" ];
      };
      cpp = {
        command = "${llvm.clang-tools}/bin/clang-format";
        options = [ "-i" ];
        includes = [
          "pkgs/*/*/src/*.cc"
          "pkgs/*/*/src/*.h"
          "pkgs/*/*/src/*.c"
        ];
      };
      # check only: clang-tidy with /.clang-tidy, warnings are errors
      cpp-tidy = {
        command = "${import ./pkgs/ji/jig/tidy.nix { inherit pkgs llvm; }}/bin/jig-tidy";
        includes = [ "pkgs/*/*/src/*.cc" ];
      };
      python = {
        command = "${pkgs.ruff}/bin/ruff";
        options = [ "format" ];
        includes = [ "*.py" ];
      };
      python-lint = {
        command = "${pkgs.ruff}/bin/ruff";
        options = [
          "check"
          "--fix"
        ];
        includes = [ "*.py" ];
      };
      toml = {
        command = "${pkgs.taplo}/bin/taplo";
        options = [ "format" ];
        includes = [ "*.toml" ];
        excludes = [ "locks/*.toml" ];
      };
      # one entry per line, sorted, so additions merge textually (.gitattributes merge=union)
      locks = {
        command = "${pkgs.nushell}/bin/nu";
        options = [
          "--no-config-file"
          "pkgs/up/uptrack/src/locks.nu"
        ];
        includes = [ "locks/*.toml" ];
      };
      # nu has no stable formatter, this only checks (parse + types with the nu that runs builds).
      nu-typecheck = {
        command = "${nu-typecheck}";
        includes = nuFiles;
      };
      # the nu that package.nix assembles from package.nix, its modules and the build systems
      package-scripts = pkgs.lib.mkIf evaluates {
        command = "${pkgs.nushell}/bin/nu";
        options = [
          "--no-config-file"
          "lints/package-scripts.nu"
        ];
        includes = [
          "pkgs/*/*/package.nix"
        ];
      };
      nu-ast-grep = {
        command = "${pkgs.ast-grep}/bin/ast-grep";
        options = [
          "scan"
          "--config=${sgconfig}"
          "--report-style=short"
        ];
        includes = nuFiles ++ [ "pkgs/*/*/*.nix" ];
      };
    };
  };
}
