# nixbot builds .#checks (nix/checks.nix), .#packages is the whole set. The package set itself is
# default.nix and takes no inputs; nixpkgs is what treefmt and the seed build already use.
{
  inputs.nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";

  outputs =
    { nixpkgs, ... }:
    let
      # aarch64-linux once its seed nar is uploaded (pkgs/se/seed/upload.nu)
      systems = [ "x86_64-linux" ];
      each = f: nixpkgs.lib.genAttrs systems f;
    in
    {
      packages = each (
        system:
        import ./default.nix { inherit system; }
        // {
          nix = import ./nix/nix { pkgs = nixpkgs.legacyPackages.${system}; };
        }
      );
      # `repkgs.lib.<system>.mkShell { packages = [ … ]; }` for a flake that wants a dev shell of
      # this set's tools. Per system, because a package of the set is.
      lib = each (
        system:
        let
          s = import ./nix/set.nix { inherit system; };
        in
        {
          inherit (s) mkShell mkShellNoCC;
        }
      );
      checks = each (system: import ./nix/checks.nix { inherit system nixpkgs; });
      formatter = each (system: import ./treefmt.nix { pkgs = nixpkgs.legacyPackages.${system}; });
      devShells = each (system: {
        default = import ./shell.nix { pkgs = nixpkgs.legacyPackages.${system}; };
      });
    };
}
